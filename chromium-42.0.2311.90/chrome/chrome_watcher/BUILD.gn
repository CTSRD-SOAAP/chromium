# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/version.gni")

source_set("client") {
  sources = [
    "chrome_watcher_main_api.cc",
    "chrome_watcher_main_api.h",
  ]
  deps = [
    "//base",
  ]
}

process_version("chrome_watcher_resources") {
  source = "//chrome/app/chrome_version.rc.version"
  output = "$target_gen_dir/chrome_watcher_version.rc"
  extra_args = [
    "-f",
    rebase_path("chrome_watcher.ver", root_build_dir),
  ]
}

shared_library("chrome_watcher") {
  sources = [
    "$target_gen_dir/chrome_watcher_version.rc",
    "chrome_watcher_main.cc",
  ]
  inputs = [
    "chrome_watcher.def",
  ]
  deps = [
    ":chrome_watcher_resources",
    ":client",
    "//base",
    "//components/browser_watcher",
  ]
  ldflags = [ "/DEF:" + rebase_path("chrome_watcher.def", root_build_dir) ]
  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]
}
